/*
 * Copyright (C) 2014 Andrew Comminos
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}
apply plugin: 'com.android.library'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.4.0'
    api 'com.madgag.spongycastle:core:1.51.0.0'

    // Custom PKCS12 keybag parse modifications to support Mumble unencrypted certificates
    // Source: https://github.com/Morlunk/spongycastle/tree/pkcs12-keybag-fixes
    api files('libs/sc-morlunk-prov-1.51.0.0.jar', 'libs/sc-morlunk-pkix-1.51.0.0.jar')

    implementation 'com.googlecode.javacpp:javacpp:0.7'
    implementation 'com.intellij:annotations:12.0@jar'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

}

def getNdkPath() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    if (ndkdir == null)
        throw new GradleException('NDK location not found. Define location with "ndk.dir" in the "local.properties" file.')
    return ndkdir
}

def getNdkBuildCmd() {
    def ndkbuild = getNdkPath() + File.separator + "ndk-build"
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        ndkbuild = ndkbuild + ".cmd"
    }
    return ndkbuild
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    sourceSets {
        main {
            res.srcDirs = ['src/main/res']
            jniLibs.srcDirs = ['src/main/libs/']
            jni.srcDirs = [] // This prevents the auto generation of Android.mk
        }
    }

    defaultConfig {
        testApplicationId "com.morlunk.jumble.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        minSdkVersion 16
        targetSdkVersion 28

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            stl "gnustl_static"
            cFlags "-I\$(LOCAL_PATH)/speex/include/ -I\$(LOCAL_PATH)/celt-0.11.0-src/include/ -I\$(LOCAL_PATH)/celt-0.7.0-src/include/ -I\$(LOCAL_PATH)/opus/include -D__EMX__ -DUSE_KISS_FFT -DFIXED_POINT -DEXPORT='' -DHAVE_CONFIG_H -fvisibility=hidden -DOPUS_BUILD -DVAR_ARRAYS -Wno-traditional -DFIXED_POINT"
        }
    }

    // Trigger NDK build on java compilation task.
    task ndkBuild(type: Exec) {
        commandLine getNdkBuildCmd(), '-C', file('src/main/jni/').absolutePath
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}
